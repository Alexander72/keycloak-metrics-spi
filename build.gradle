plugins {
    id "net.nemerosa.versioning" version "2.8.2"
}

repositories {
    jcenter()
}

configurations {
    bundleLib
}

group 'org.jboss.aerogear'
version '1.0.0'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.keycloak', name: 'keycloak-server-spi-private', version: '3.2.1.Final'
    compile group: 'org.keycloak', name: 'keycloak-server-spi', version: '3.2.1.Final'
    compile group: 'org.keycloak', name: 'keycloak-services', version: '3.2.1.Final'
    compile group: 'org.jboss.spec.javax.ws.rs', name: 'jboss-jaxrs-api_2.0_spec', version: '1.0.0.Final'
    bundleLib group: 'io.prometheus', name: 'simpleclient_common', version: '0.3.0'
    bundleLib group: 'io.prometheus', name: 'simpleclient_hotspot', version: '0.3.0'
    configurations.compile.extendsFrom(configurations.bundleLib)

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from {
        configurations.bundleLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Version'        : "${gradle.rootProject.version}"
        )
    }
}
